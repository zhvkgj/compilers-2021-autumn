-- Stack machine.

import List;
import World;
import State;
import Expr;
import Buffer;
import Fun;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ      -> sprintf ("READ")
  | WRITE     -> sprintf ("WRITE")
  | BINOP (s) -> sprintf ("BINOP %s", s)
  | LD    (x) -> sprintf ("LD %s", x)
  | ST    (x) -> sprintf ("ST %s", x)
  | CONST (n) -> sprintf ("CONST %d", n)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Stack machine's single instruction interpreter. Takes an SM-configuration and a instruction,
-- returns a processed configuration
fun evalInsn(c, insn) {
  case insn of
    READ      -> var readed = readWorld (c[2]);
                 [readed.fst : c[0], c[1], readed.snd]

  | WRITE     -> var x = hd (c[0]);
                 var newStack = tl (c[0]);
                 [newStack, c[1], writeWorld (x, c[2])]

  | BINOP (s) -> var y = hd (c[0]);
                 var x = hd (tl (c[0]));
                 var stackTail = tl (tl (c[0]));
                 var res = binop (s) (x, y);
                 [res : stackTail, c[1], c[2]]

  | LD    (x) -> var state = c[1];
                 [state (x) : c[0], state, c[2]]

  | ST    (x) -> var newStack = tl (c[0]);
                 var head = hd (c[0]);
                 [newStack, c[1] <- [x, head] , c[2]]

  | CONST (n) -> [n : c[0], c[1], c[2]]
  esac
}

-- Stack machine interpreter. Takes an SM-configuration and a program,
-- returns a final configuration
fun eval (c, insns) {
  foldl(evalInsn, c, insns)
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval ([{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list 
-- of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Const (n)        -> {CONST (n)}
  | Var   (x)        -> {LD (x)}
  | Binop (op, l, r) -> compileExpr (l) +++ compileExpr (r) +++ {BINOP (op)}
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  case stmt of
    Assn  (x, e)         -> compileExpr (e) +++ {ST (x)}
  | Read  (x)            -> {READ, ST (x)}
  | Write (e)            -> compileExpr (e) +++ {WRITE}
  | Seq   (stmt1, stmt2) -> compileSM (stmt1) +++ compileSM (stmt2)
  | Skip                 -> {}
  esac
}
